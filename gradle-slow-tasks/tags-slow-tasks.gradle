// Imagine you have a build that, sometimes (but not always), takes much longer than expected.
// You've looked at some build scans and determined the issue is the JavaCompile task.
// You can add the following snippet to your root build.gradle script to tag those builds that
// take "too long" to build. Later on, as a Gradle Enterprise user, you can filter your scans
// list to see just those builds that contained a JavaCompile task that took "too long".
// Borrowed from: https://blog.gradle.org/build-scans-tag-root-cause

def THRESHOLD_MILLIS = 15 * 60 * 1000 // 15 min

allprojects {
    tasks.matching { t ->
        t instanceof JavaCompile
    }.configureEach {
        long start
        doFirst {
            start = System.currentTimeMillis()
        }
        doLast {
            long duration = System.currentTimeMillis() - start
            if (duration > THRESHOLD_MILLIS) {
                buildScan.tag 'TooLong'
                buildScan.value 'TooLong', path
            }
        }
    }
}
